# Maintainer: Hector Martin <marcan@marcan.st>

_version=1.4.19
_commitdate=20250125
_commitid=168de834d8c132049e058aff2fb6425dfb55d60d

pkgname=m1n1-git
pkgver=${_version}+git${_commitdate}.${_commitid:0:7}
pkgrel=1
pkgdesc='Asahi Linux bootloader'
_artwork_commit_id=35626c31f5c5a8a884aaff93f864795026495742
_artwork_srcname=artwork-${_artwork_commit_id}
arch=('aarch64')
url='http://asahilinux.org'
license=('MIT')
makedepends=( imagemagick git )
conflicts=('m1n1')
replaces=('m1n1')
provides=("m1n1=${_version}")
source=(
  "artwork-${_artwork_commit_id}.tar.gz::https://github.com/AsahiLinux/artwork/archive/${_artwork_commit_id}.tar.gz"
  "git+https://github.com/AsahiLinux/m1n1.git#commit=$_commitid"
  "fix-makefile"
)
sha256sums=('ffa129b5cae8debd4ffd97f7f1ac7f571c7ba4679793cd138918c2ae76a7103d'
            SKIP
            'b3e99a0a63542666ede6cd5994560256f43b7b7d79540c166759796f43868e05'
)
b2sums=('26e9668f7c7393fec24b53b184ea0eabed4f46479c1aab26728d01c179091683cc4387ff83796bdcbb289376236faf9f651197b9fbba51e52540e58ebdc8bcc5'
        SKIP
        'eb1a4d24258bf5d4150d9101ce674afb57e801b9002711f59daae5d700a4a7af94d571ae7dedc59a9106dc95d7f0b5b35b6b0137cc87933e7d1dd323945cf6ca'
)

prepare() {
  rm -rf "${srcdir}"/m1n1/artwork
  cp -r "${srcdir}/$_artwork_srcname" "${srcdir}"/m1n1/artwork

  cd "${srcdir}"/m1n1
  # apply patches/modify sources as needed

  patch -p1 < "${srcdir}"/fix-makefile
}

build() {
  cd "${srcdir}"/m1n1
  make ARCH= RELEASE=1
}

package() {
  cd "${srcdir}"

  tgtdir="$pkgdir/usr/lib/asahi-boot"

  install -Dt "$tgtdir" -m644 m1n1/build/m1n1.bin

  install -Dm644 "$srcdir"/m1n1/LICENSE \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
