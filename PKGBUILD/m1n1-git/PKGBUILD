# Maintainer: Hector Martin <marcan@marcan.st>

_version=1.3.3
_commitdate=20230821
_commitid=6d0979e71e83f47c5da5fdb8c5e21eb1268d54e8

pkgname=m1n1-git
pkgver=${_version}+git${_commitdate}.${_commitid:0:7}
pkgrel=1
pkgdesc='Asahi Linux bootloader'
_artwork_commit_id=35626c31f5c5a8a884aaff93f864795026495742
_artwork_srcname=artwork-${_artwork_commit_id}
arch=('aarch64')
url='http://asahilinux.org'
license=('MIT')
makedepends=( imagemagick git )
conflicts=('m1n1')
replaces=('m1n1')
provides=("m1n1=${_version}")
source=(
  "artwork-${_artwork_commit_id}.tar.gz::https://github.com/AsahiLinux/artwork/archive/${_artwork_commit_id}.tar.gz"
  "git+https://github.com/AsahiLinux/m1n1.git#commit=$_commitid"
)
sha256sums=('ffa129b5cae8debd4ffd97f7f1ac7f571c7ba4679793cd138918c2ae76a7103d'
            SKIP)
b2sums=('26e9668f7c7393fec24b53b184ea0eabed4f46479c1aab26728d01c179091683cc4387ff83796bdcbb289376236faf9f651197b9fbba51e52540e58ebdc8bcc5'
        SKIP)

prepare() {
  rm -rf "${srcdir}"/m1n1/artwork
  cp -r "${srcdir}/$_artwork_srcname" "${srcdir}"/m1n1/artwork
}

build() {
  cd "${srcdir}"/m1n1
  make ARCH= RELEASE=1
}

package() {
  cd "${srcdir}"

  tgtdir="$pkgdir/usr/lib/asahi-boot"

  install -Dt "$tgtdir" -m644 m1n1/build/m1n1.bin

  install -Dm644 "$srcdir"/m1n1/LICENSE \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
