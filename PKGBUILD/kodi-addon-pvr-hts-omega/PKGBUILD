# Maintainer: Daniel Scheller <d.scheller@gmx.net>

_version=21.2.0
_commitdate=20231220
_commitid=f336890562972b5a5d0a1cf6b363ff10262fcbd5

pkgname=kodi-addon-pvr-hts-omega
pkgver=${_version}+git${_commitdate}.${_commitid:0:7}
pkgrel=1
pkgdesc="HTS/TVHeadend PVR addon for Kodi"
arch=('x86_64' 'i686' 'aarch64' 'armv7h' 'armv6h')
url="https://github.com/kodi-pvr/pvr.hts"
license=('GPL2')
depends=('kodi-omega')
makedepends=('kodi-omega-dev' 'git' 'cmake')
provides=("${pkgname%-omega}")
conflicts=("${pkgname%-omega}" "${pkgname%-omega}-git")
source=("$pkgname::git+https://github.com/kodi-pvr/pvr.hts.git#commit=$_commitid")
b2sums=('SKIP')

build() {
  mkdir -p "$srcdir/$pkgname/$pkgname-build"
  cd "$srcdir/$pkgname/$pkgname-build"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_BUILD_TYPE=Release \
           -DBUILD_SHARED_LIBS=1 \
           -DUSE_LTO=1
  make
}

package() {
  cd "$srcdir/$pkgname/$pkgname-build"
  make DESTDIR="$pkgdir/" install
}
