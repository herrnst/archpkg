# Maintainer: Christer Solskogen <christer.solskogen@gmail.com>

pkgname=sdl2
pkgver=2.24.0
pkgrel=1
pkgdesc="A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard (Version 2)"
arch=('x86_64' 'aarch64' 'armv7h')
url="https://www.libsdl.org"
license=('MIT')
depends=('glibc' 'libxext' 'libxrender' 'libx11' 'libgl' 'libxcursor' 'hidapi' 'libusb')
makedepends=('alsa-lib' 'mesa' 'libpulse' 'libxrandr' 'libxinerama' 'wayland' 'libxkbcommon'
             'wayland-protocols' 'ibus' 'fcitx5' 'libxss' 'cmake' 'jack' 'ninja' 'pipewire'
	     'libdecor' 'git')
optdepends=('alsa-lib: ALSA audio driver'
            'libpulse: PulseAudio audio driver'
            'jack: JACK audio driver'
	    'pipewire: PipeWire audio driver'
	    'libdecor: Wayland client decorations')
source=("https://github.com/libsdl-org/SDL/archive/refs/tags/release-2.24.0.tar.gz")
provides=("sdl2=$pkgver")
conflicts=(sdl2-git sdl2-minimal-hg)
sha512sums=('a5a7fc1403c88a3452e3c663487a2ea52b194ce7bd5877a94c2608cc0343c4149cd7b675408d23ed32960dd21b9460d4f1f3dbc07e45ed1ff1ea67fa1e359f27')

build() {
	CFLAGS+=" -ffat-lto-objects"
	cmake -S SDL-release-${pkgver} -B build -G Ninja \
	-D CMAKE_INSTALL_PREFIX=/usr \
	-D SDL_RPI=OFF \
	-D SDL_STATIC=OFF \
	-D SDL_RPATH=OFF
	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
	# For some reason, this isn't named correctly and we have to fix it to reflect the actual staticlib name.
	sed -i "s/libSDL2\.a/libSDL2main.a/g" "$pkgdir"/usr/lib/cmake/SDL2/SDL2Targets-noconfig.cmake
	install -Dm644 SDL-release-${pkgver}/LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

	# remove duplicate license file
	rm "$pkgdir/usr/share/licenses/SDL2/LICENSE.txt"
	rmdir "$pkgdir/usr/share/licenses/SDL2"

	chown -R root:root "$pkgdir"
}

