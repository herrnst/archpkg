# Maintainer: Hector Martin <marcan@marcan.st>

pkgname=uboot-asahi
_ver=2024.04
_commitdate=20240418
_commitid=e80039fbccbca268d9b48c9c0deb84d2827a4c6a

pkgver=${_ver}+git${_commitdate}.${_commitid:0:7}
pkgrel=1
pkgdesc='U-Boot for Apple Silicon Macs'
_srcname=u-boot-${_commitid}
arch=('aarch64')
url='http://asahilinux.org'
license=('MIT' 'GPL2')
makedepends=( bc imagemagick )
source=(
  "u-boot-${pkgver}.tar.gz::https://github.com/AsahiLinux/u-boot/archive/${_commitid}.tar.gz"
)
sha256sums=('9b2bf09173a35f516c3849fd07cad0d855f8b253ddc08eac0caa1730d79f9fda')
b2sums=('ab9babc1b8147f4adb4b5e587775d5f76667d062a39333f30527b717c1a7798eea6a6cd27999e1645bbdbd64421c9fc409e1f3794f3b2c16c4d8458d7b63eb6a')

prepare() {
  cd "${srcdir}/$_srcname"
  make apple_m1_defconfig
}

build() {
  cd "${srcdir}/$_srcname"
  make
}

package() {
  cd "${srcdir}"

  tgtdir="$pkgdir/usr/lib/asahi-boot"

  install -Dt "$tgtdir" -m644 "$_srcname/u-boot-nodtb.bin"
  install -Dt "$tgtdir/dtb" -m644 "$_srcname/arch/arm/dts/"t[86]*.dtb
}
