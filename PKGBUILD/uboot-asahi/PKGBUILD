# Maintainer: Hector Martin <marcan@marcan.st>

pkgname=uboot-asahi
_ver=2024.07
_commitdate=20240824
_commitid=5e1506a651a55407ddccfeb60687edf3e3abaea9

pkgver=${_ver}+git${_commitdate}.${_commitid:0:7}
pkgrel=1
pkgdesc='U-Boot for Apple Silicon Macs'
_srcname=u-boot-${_commitid}
arch=('aarch64')
url='http://asahilinux.org'
license=('MIT' 'GPL2')
makedepends=( bc imagemagick )
source=(
  "u-boot-${pkgver}.tar.gz::https://github.com/AsahiLinux/u-boot/archive/${_commitid}.tar.gz"
)
sha256sums=('256692639eaf1dec89d9f6b2986e652becc2d13f456e8a9131f7a863f37eb2ac')
b2sums=('bc92740167aaee90388f8f38d720e45f2cfb7b2b29533bd15d6a62544caef850acd60c0b4843f2c0f1f04363ac7556fb3e03b3fdab2a6e17516e6e51712a7236')

prepare() {
  cd "${srcdir}/$_srcname"
  make apple_m1_defconfig
}

build() {
  cd "${srcdir}/$_srcname"
  make
}

package() {
  cd "${srcdir}"

  tgtdir="$pkgdir/usr/lib/asahi-boot"

  install -Dt "$tgtdir" -m644 "$_srcname/u-boot-nodtb.bin"
  install -Dt "$tgtdir/dtb" -m644 "$_srcname/arch/arm/dts/"t[86]*.dtb
}
