# Maintainer: Hector Martin <marcan@marcan.st>

pkgname=uboot-asahi
_ver=2024.10
_commitdate=20241029
_commitid=4b6e0ff9087fba59f189b8f0ca3c3f6cf26e704f

pkgver=${_ver}+git${_commitdate}.${_commitid:0:7}
pkgrel=1
pkgdesc='U-Boot for Apple Silicon Macs'
_srcname=u-boot-${_commitid}
arch=('aarch64')
url='http://asahilinux.org'
license=('MIT' 'GPL2')
makedepends=( bc imagemagick )
source=(
  "u-boot-${pkgver}.tar.gz::https://github.com/AsahiLinux/u-boot/archive/${_commitid}.tar.gz"
)
sha256sums=('97bd414926166e91b4a7594de6cf9ea6c2573308af474470270d0c58a0683c67')
b2sums=('dbe047ed8832d56b3088642e73f38dc91ab17c00031d02dd69e5b392278295ba767ff3736a27adb488f38174223ac5a3f0a9f70ec84ae83cb173920c32e13648')

prepare() {
  cd "${srcdir}/$_srcname"
  make apple_m1_defconfig
}

build() {
  cd "${srcdir}/$_srcname"
  make
}

package() {
  cd "${srcdir}"

  tgtdir="$pkgdir/usr/lib/asahi-boot"

  install -Dt "$tgtdir" -m644 "$_srcname/u-boot-nodtb.bin"
  install -Dt "$tgtdir/dtb" -m644 "$_srcname/arch/arm/dts/"t[86]*.dtb
}
