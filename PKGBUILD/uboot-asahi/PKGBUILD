# Maintainer: Hector Martin <marcan@marcan.st>

pkgname=uboot-asahi
_ver=2025.10
_commitdate=20260123
_commitid=4411224f218ee67526f74d10cd3b3a7cd70ff2ad

pkgver=${_ver}+git${_commitdate}.${_commitid:0:7}
pkgrel=1
pkgdesc='U-Boot for Apple Silicon Macs'
_srcname=u-boot-${_commitid}
arch=('aarch64')
url='http://asahilinux.org'
license=('MIT' 'GPL2')
makedepends=( bc imagemagick )
source=(
  "u-boot-${pkgver}.tar.gz::https://github.com/AsahiLinux/u-boot/archive/${_commitid}.tar.gz"
)
sha256sums=('ecb9f772f50e459f9e6586ca69ac79873c58115892110162321b4197ee3d56a8')
b2sums=('7dfab60c76ae7bb83a021e2ff90eb77821999057a9864afa5579c3767ecaf21d91b11aafac36a22e37c3311b0ec4b982cb5608fcbf48e2660db546e369548fff')

prepare() {
  cd "${srcdir}/$_srcname"
  make apple_m1_defconfig
}

build() {
  cd "${srcdir}/$_srcname"
  make
}

package() {
  cd "${srcdir}"

  tgtdir="$pkgdir/usr/lib/asahi-boot"

  install -Dt "$tgtdir" -m644 "$_srcname/u-boot-nodtb.bin"
  install -Dt "$tgtdir/dtb" -m644 "$_srcname/arch/arm/dts/"t[86]*.dtb
}
