# Maintainer: Daniel Scheller <d.scheller@gmx.net>

_version=21.8.1
_commitdate=20231220
_commitid=f8927e29d083d9590bd1cdef6c7fc8f3d31ef75b

pkgname=kodi-addon-pvr-magenta-omega
pkgver=${_version}+git${_commitdate}.${_commitid:0:7}
pkgrel=1
pkgdesc="MagentaTV PVR addon for Kodi"
arch=('x86_64' 'i686' 'aarch64' 'armv7h' 'armv6h')
url="https://github.com/nirvana-7777/pvr.magenta"
license=('GPL2')
depends=('kodi-omega' 'kodi-addon-inputstream-adaptive-omega')
makedepends=('kodi-omega-dev' 'git' 'cmake' 'rapidjson')
provides=("${pkgname%-omega}")
conflicts=("${pkgname%-omega}" "${pkgname%-omega}-git")
source=("$pkgname::git+https://github.com/nirvana-7777/pvr.magenta.git#commit=$_commitid")
b2sums=('SKIP')

build() {
  mkdir -p "$srcdir/$pkgname/$pkgname-build"
  cd "$srcdir/$pkgname/$pkgname-build"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_BUILD_TYPE=Release \
           -DBUILD_SHARED_LIBS=1 \
           -DUSE_LTO=1
  make
}

package() {
  cd "$srcdir/$pkgname/$pkgname-build"
  make DESTDIR="$pkgdir/" install
}
