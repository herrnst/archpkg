# Maintainer: Daniel Scheller <d.scheller@gmx.net>

_version=21.9.2
_commitdate=20240410
_commitid=6da67596cc7f49dd1bc6d0e2e1ae0b5594e31052

pkgname=kodi-addon-pvr-magenta-omega
pkgver=${_version}+git${_commitdate}.${_commitid:0:7}
pkgrel=1
pkgdesc="MagentaTV PVR addon for Kodi"
arch=('x86_64' 'i686' 'aarch64' 'armv7h' 'armv6h')
url="https://github.com/nirvana-7777/pvr.magenta"
license=('GPL2')
depends=('kodi-omega' 'kodi-addon-inputstream-adaptive-omega')
makedepends=('kodi-omega-dev' 'git' 'cmake' 'rapidjson')
provides=("${pkgname%-omega}")
conflicts=("${pkgname%-omega}" "${pkgname%-omega}-git")
source=(
	"$pkgname::git+https://github.com/nirvana-7777/pvr.magenta.git#commit=$_commitid"
	'fixtinyxml.patch'
)
b2sums=(
	'SKIP'
	'4c2d32d6c4561b85944381c72944fe6dd3ac0095807b3d4ba35ea572805e499781fcecc976f225ebcbb1bcd31464848cd3c85175ed9c50af7c6a874bee135e36'
)

prepare() {
  [[ -d "$srcdir/$pkgname/$pkgname-build" ]] && rm -rf "$srcdir/$pkgname/$pkgname-build"
  mkdir -p "$srcdir/$pkgname/$pkgname-build"

  cd "$srcdir/$pkgname"
  patch -p1 -i "$srcdir/fixtinyxml.patch"
}

build() {
  cd "$srcdir/$pkgname/$pkgname-build"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_BUILD_TYPE=Release \
           -DBUILD_SHARED_LIBS=1 \
           -DUSE_LTO=1
  make
}

package() {
  cd "$srcdir/$pkgname/$pkgname-build"
  make DESTDIR="$pkgdir/" install
}
