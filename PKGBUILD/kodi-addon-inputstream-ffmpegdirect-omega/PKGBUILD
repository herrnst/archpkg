# Maintainer: Daniel Scheller <d.scheller@gmx.net>

_version=21.3.5
_commitdate=20240512
_commitid=f81ec9fd1171a6af927f66eef910c18a5c997514

pkgname=kodi-addon-inputstream-ffmpegdirect-omega
pkgver=${_version}+git${_commitdate}.${_commitid:0:7}
pkgrel=2
pkgdesc="FFmpegdirect Inputstream addon for Kodi"
arch=('x86_64' 'i686' 'aarch64' 'armv7h' 'armv6h')
url="https://github.com/xbmc/inputstream.ffmpegdirect"
license=('GPL2')
depends=('kodi-omega' 'zlib' 'bzip2' 'libva')
makedepends=('kodi-omega-dev' 'kodi-omega-ffmpeg-dev' 'pkgconf' 'git' 'cmake' 'yasm' 'zlib' 'bzip2')
provides=("${pkgname%-omega}")
conflicts=("${pkgname%-omega}" "${pkgname%-omega}-git")
source=(
	"$pkgname::git+https://github.com/xbmc/inputstream.ffmpegdirect.git#commit=$_commitid"
	"pr304-ffmpeg7.patch"
)
sha256sums=(
	'SKIP'
	'ae8c733b6cc3b1cc510d252be2ff680f44a19e44a2a98ba2566594f1a63003ce'
)

prepare() {
  [[ -d "$srcdir/$pkgname/$pkgname-build" ]] && rm -rf "$srcdir/$pkgname/$pkgname-build"
  mkdir -p "$srcdir/$pkgname/$pkgname-build"

  cd "$srcdir/$pkgname"
  patch -p1 -i "$srcdir/pr304-ffmpeg7.patch"
}

build() {
  cd "$srcdir/$pkgname/$pkgname-build"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_BUILD_TYPE=Release \
           -DBUILD_SHARED_LIBS=1 \
           -DCMAKE_PREFIX_PATH=/usr/lib/kodi/ffmpeg \
           -DUSE_LTO=1
  make
}

package() {
  cd "$srcdir/$pkgname/$pkgname-build"
  make DESTDIR="$pkgdir/" install
}
