# Maintainer: Daniel Scheller <d.scheller@gmx.net>

_version=21.0.0
_commitdate=20230211
_commitid=feb99be0835cdd17aa681dae522c2677ab8f42f4

pkgname=kodi-addon-audiodecoder-sacd-omega
pkgver=${_version}+git${_commitdate}.${_commitid:0:7}
pkgrel=1
pkgdesc="SACD audiodecoder addon for Kodi"
arch=('x86_64' 'i686' 'aarch64' 'armv7h' 'armv6h')
url="https://github.com/xbmc/audiodecoder.sacd"
license=('GPL2')
depends=('kodi-omega' 'wavpack')
makedepends=('kodi-omega-dev' 'git' 'cmake' 'wavpack')
provides=("${pkgname%-omega}")
conflicts=("${pkgname%-omega}" "${pkgname%-omega}-git")
source=("$pkgname::git+https://github.com/xbmc/audiodecoder.sacd.git#commit=$_commitid")
b2sums=('SKIP')

build() {
  mkdir -p "$srcdir/$pkgname/$pkgname-build"
  cd "$srcdir/$pkgname/$pkgname-build"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_BUILD_TYPE=Release \
           -DBUILD_SHARED_LIBS=1 \
           -DUSE_LTO=1
  make
}

package() {
  cd "$srcdir/$pkgname/$pkgname-build"
  make DESTDIR="$pkgdir/" install
}
