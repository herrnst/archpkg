# Maintainer: Frederic Bezies <fredbezies at gmail dot com>

_tagver=2026.01.02
_commitdate=20260204
_commitid=fe47cfb20de17f9cf3c3a4382aa23a29e090e102

pkgname=dosbox-x-sdl2
pkgver=${_tagver}+git${_commitdate}.${_commitid:0:7}
pkgrel=1
pkgdesc="x86 emulator with builtin DOS, with patches and more features"
arch=(i686 x86_64 aarch64)
url="http://dosbox-x.com"
license=(GPL)
depends=(fluidsynth libxkbfile libxrandr ffmpeg sdl2_net libslirp)
makedepends=(git glu nasm)
optdepends=()
conflicts=('dosbox-x-git' 'dosbox-x' 'dosbox-x-sdl2-git')
source=(
	"$pkgname::git+https://github.com/joncampbell123/dosbox-x.git#commit=$_commitid"
	'001-mt32_aarch64.patch'
	'revertcmos.patch'
)
sha256sums=(
	'SKIP'
	'413c25ec5242d14ac6406c1b13ef7097f9b7ae85c069624318ad4252e8d9a7f7'
	'fe5e04c75ecb654db78ea23bdf7fbb6e1abda16da4700c0f8edbd3e973266242'
)

build() {
  cd "$srcdir/$pkgname"

  # Enable MT32 on aarch64
  patch -p1 < ../001-mt32_aarch64.patch

  # Revert possibly problematic CMOS changes (which at least break Windows NT)
  patch -p1 < ../revertcmos.patch

  ./update-build-timestamp.pl
  ./autogen.sh

  chmod +x configure
  ./configure --enable-core-inline --disable-debug --enable-avcodec --prefix=/usr --enable-sdl2
  make -j$(nproc)
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir" install
}
