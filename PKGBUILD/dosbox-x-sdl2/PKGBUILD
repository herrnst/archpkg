# Maintainer: Frederic Bezies <fredbezies at gmail dot com>

_tagver=2023.03.31
pkgname=dosbox-x-sdl2
pkgver=0.84.3+$_tagver
pkgrel=1
pkgdesc="x86 emulator with builtin DOS, with patches and more features"
arch=(i686 x86_64 aarch64)
url="http://dosbox-x.com"
license=(GPL)
depends=(fluidsynth libxkbfile libxrandr ffmpeg sdl2_net libslirp)
makedepends=(glu nasm)
optdepends=()
conflicts=('dosbox-x-git' 'dosbox-x' 'dosbox-x-sdl2-git')
source=(
	"https://github.com/joncampbell123/dosbox-x/archive/dosbox-x-v$_tagver.tar.gz"
	'001-mt32_aarch64.patch'
)
sha256sums=(
	'233f487a90654b03f32b290579f24d5b188a65361b8a7787154f5c4cd8f5f144'
	'413c25ec5242d14ac6406c1b13ef7097f9b7ae85c069624318ad4252e8d9a7f7'
)

build() {
  cd "$srcdir/dosbox-x-dosbox-x-v$_tagver"

  # Enable MT32 on aarch64
  patch -p1 < ../001-mt32_aarch64.patch

  ./autogen.sh
  export LDFLAGS="${LDFLAGS//,--as-needed}"
  chmod +x configure
  ./configure --enable-core-inline --disable-debug --enable-avcodec --prefix=/usr --enable-sdl2
  make -j$(nproc)
}

package() {
  cd "$srcdir/dosbox-x-dosbox-x-v$_tagver"
  make DESTDIR="$pkgdir" install
}
