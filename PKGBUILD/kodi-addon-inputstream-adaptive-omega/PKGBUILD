# Maintainer: Daniel Scheller <d.scheller@gmx.net>

_version=21.5.2
_commitdate=20240811
_commitid=f2cadb0ff6212a70ae38ee864c7e997ec152ed52
_bento4_version=1.6.0-641-3-Omega

pkgname=kodi-addon-inputstream-adaptive-omega
pkgver=${_version}+git${_commitdate}.${_commitid:0:7}
pkgrel=1
pkgdesc="Inputstream Adaptive addon for Kodi"
arch=('x86_64' 'i686' 'aarch64' 'armv7h' 'armv6h')
url="https://github.com/xbmc/inputstream.adaptive"
license=('GPL2')
depends=('kodi-omega' 'expat' 'rapidjson')
makedepends=('kodi-omega-dev' 'git' 'cmake' 'gtest' 'rapidjson')
provides=("${pkgname%-omega}")
conflicts=("${pkgname%-omega}" "${pkgname%-omega}-git")
# kodi 20
source=("$pkgname::git+https://github.com/xbmc/inputstream.adaptive.git#commit=$_commitid"
	"bento4-$_bento4_version.tar.gz::https://github.com/xbmc/Bento4/archive/refs/tags/$_bento4_version.tar.gz"
)
b2sums=(
	'SKIP'
	'ae38a7f0f155efb3e31f2fb3ffeec29ae68ed524307c277e3b522ee60b0d7fa1ae8b35931837bddbba90d91c69ce51b81c7ecf9c3559ca5d6fde401aacb36327'
)

prepare() {
  [[ -d "$srcdir/$pkgname/$pkgname-build" ]] && rm -rf "$srcdir/$pkgname/$pkgname-build"
  mkdir -p "$srcdir/$pkgname/$pkgname-build"

  # add whatever source preparation steps are needed, ie. apply patches
  #cd "$srcdir/$pkgname"
  #patch -p1 -i patchfile
}

build() {
  cd "$srcdir/$pkgname/$pkgname-build"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_BUILD_TYPE=Release \
           -DBUILD_SHARED_LIBS=1 \
           -DENABLE_INTERNAL_BENTO4=ON \
           -DBENTO4_URL="$srcdir/bento4-$_bento4_version.tar.gz" \
           -DUSE_LTO=1
  make
}

package() {
  cd "$srcdir/$pkgname/$pkgname-build"
  make DESTDIR="$pkgdir/" install
}
