# Maintainer: Daniel Scheller <d.scheller@gmx.net>

_version=21.4.0
_commitdate=20231201
_commitid=aed0903e84cfe115c59f87acfddc2734004c1831
_bento4_version=1.6.0-639-8-Omega

pkgname=kodi-addon-inputstream-adaptive-omega
pkgver=${_version}+git${_commitdate}.${_commitid:0:7}
pkgrel=1
pkgdesc="Inputstream Adaptive addon for Kodi"
arch=('x86_64' 'i686' 'aarch64' 'armv7h' 'armv6h')
url="https://github.com/xbmc/inputstream.adaptive"
license=('GPL2')
depends=('kodi-omega' 'expat')
makedepends=('kodi-omega-dev' 'git' 'cmake' 'gtest')
provides=("${pkgname%-omega}")
conflicts=("${pkgname%-omega}" "${pkgname%-omega}-git")
# kodi 20
source=("$pkgname::git+https://github.com/xbmc/inputstream.adaptive.git#commit=$_commitid"
	"bento4-$_bento4_version.tar.gz::https://github.com/xbmc/Bento4/archive/refs/tags/$_bento4_version.tar.gz"
	"pr1399.patch"
)
b2sums=(
	'SKIP'
	'7f1b42e9643e88d39ee88e6804358789d491101d4edadc9fa152d8f2258f1dffef2c2c5a1da7928510778843019d0257924134a5d6177442c30783b7492aaa04'
	'e399366f95049e217519e50e2f2339818603f75030351f101c2574e264a5bd857e94c655e2d6955bd3bfae4731e44d87df100c7597bb31544bb3beece830bf65'
)

prepare() {
  [[ -d "$srcdir/$pkgname/$pkgname-build" ]] && rm -rf "$srcdir/$pkgname/$pkgname-build"
  mkdir -p "$srcdir/$pkgname/$pkgname-build"

  cd "$srcdir/$pkgname"
  patch -p1 -i "$srcdir/pr1399.patch"
}

build() {
  cd "$srcdir/$pkgname/$pkgname-build"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_BUILD_TYPE=Release \
           -DBUILD_SHARED_LIBS=1 \
           -DENABLE_INTERNAL_BENTO4=ON \
           -DBENTO4_URL="$srcdir/bento4-$_bento4_version.tar.gz" \
           -DUSE_LTO=1
  make
}

package() {
  cd "$srcdir/$pkgname/$pkgname-build"
  make DESTDIR="$pkgdir/" install
}
