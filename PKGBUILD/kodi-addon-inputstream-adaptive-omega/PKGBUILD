# Maintainer: Daniel Scheller <d.scheller@gmx.net>

_version=21.4.4
_commitdate=20240406
_commitid=a82e3eb8333187d8bb346bf5eca384524a553e57
_bento4_version=1.6.0-641-Omega

pkgname=kodi-addon-inputstream-adaptive-omega
pkgver=${_version}+git${_commitdate}.${_commitid:0:7}
pkgrel=1
pkgdesc="Inputstream Adaptive addon for Kodi"
arch=('x86_64' 'i686' 'aarch64' 'armv7h' 'armv6h')
url="https://github.com/xbmc/inputstream.adaptive"
license=('GPL2')
depends=('kodi-omega' 'expat')
makedepends=('kodi-omega-dev' 'git' 'cmake' 'gtest')
provides=("${pkgname%-omega}")
conflicts=("${pkgname%-omega}" "${pkgname%-omega}-git")
# kodi 20
source=("$pkgname::git+https://github.com/xbmc/inputstream.adaptive.git#commit=$_commitid"
	"bento4-$_bento4_version.tar.gz::https://github.com/xbmc/Bento4/archive/refs/tags/$_bento4_version.tar.gz"
)
b2sums=(
	'SKIP'
	'b1a92d4688731fc626327ec4900bc4808e04e496fb44d6ff711bdf427c0aa98dd785c4c9e6a31ea6f141e938199cacdaf34c7579a9b8ca58c767cf69c26d79a0'
)

prepare() {
  [[ -d "$srcdir/$pkgname/$pkgname-build" ]] && rm -rf "$srcdir/$pkgname/$pkgname-build"
  mkdir -p "$srcdir/$pkgname/$pkgname-build"

  # add whatever source preparation steps are needed, ie. apply patches
  #cd "$srcdir/$pkgname"
  #patch -p1 -i patchfile
}

build() {
  cd "$srcdir/$pkgname/$pkgname-build"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_BUILD_TYPE=Release \
           -DBUILD_SHARED_LIBS=1 \
           -DENABLE_INTERNAL_BENTO4=ON \
           -DBENTO4_URL="$srcdir/bento4-$_bento4_version.tar.gz" \
           -DUSE_LTO=1
  make
}

package() {
  cd "$srcdir/$pkgname/$pkgname-build"
  make DESTDIR="$pkgdir/" install
}
