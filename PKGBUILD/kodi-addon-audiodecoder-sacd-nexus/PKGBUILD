# Maintainer: Daniel Scheller <d.scheller@gmx.net>

_version=20.2.0
_commitdate=20220806
_commitid=82d0d1985f6ee46c7eaf736327135ce513f6f9a6

pkgname=kodi-addon-audiodecoder-sacd-nexus
pkgver=${_version}+git${_commitdate}.${_commitid:0:7}
pkgrel=1
pkgdesc="SACD audiodecoder addon for Kodi"
arch=('x86_64' 'i686' 'aarch64' 'armv7h' 'armv6h')
url="https://github.com/xbmc/audiodecoder.sacd"
license=('GPL2')
depends=('kodi-nexus' 'wavpack')
makedepends=('kodi-nexus-dev' 'git' 'cmake' 'wavpack')
provides=("${pkgname%-nexus}")
conflicts=("${pkgname%-nexus}" "${pkgname%-nexus}-git")
# kodi 20
source=("$pkgname::git+https://github.com/xbmc/audiodecoder.sacd.git#commit=$_commitid")
b2sums=('SKIP')

build() {
  mkdir -p "$srcdir/$pkgname/$pkgname-build"
  cd "$srcdir/$pkgname/$pkgname-build"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_BUILD_TYPE=Release \
           -DBUILD_SHARED_LIBS=1 \
           -DUSE_LTO=1
  make
}

package() {
  cd "$srcdir/$pkgname/$pkgname-build"
  make DESTDIR="$pkgdir/" install
}
