# Maintainer: Daniel Scheller <d.scheller@gmx.net>

_version=21.1.0
_commitdate=20240324
_commitid=bc3fcd0fa3297f160d512b7f9eec55deb42b5ef6

pkgname=kodi-addon-pvr-plutotv-omega
pkgver=${_version}+git${_commitdate}.${_commitid:0:7}
pkgrel=1
pkgdesc="Pluto.tv PVR addon for Kodi"
arch=('x86_64' 'i686' 'aarch64' 'armv7h' 'armv6h')
url="https://github.com/kodi-pvr/pvr.plutotv"
license=('GPL2')
depends=('kodi-omega' 'kodi-addon-inputstream-adaptive-omega')
makedepends=('kodi-omega-dev' 'git' 'cmake' 'rapidjson')
provides=("${pkgname%-omega}")
conflicts=("${pkgname%-omega}" "${pkgname%-omega}-git")
source=(
	"$pkgname::git+https://github.com/kodi-pvr/pvr.plutotv.git#commit=$_commitid"
)
b2sums=(
	'SKIP'
)

prepare() {
  [[ -d "$srcdir/$pkgname/$pkgname-build" ]] && rm -rf "$srcdir/$pkgname/$pkgname-build"
  mkdir -p "$srcdir/$pkgname/$pkgname-build"

  cd "$srcdir/$pkgname"
  # apply patches or any changes in general here
}

build() {
  cd "$srcdir/$pkgname/$pkgname-build"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_BUILD_TYPE=Release \
           -DBUILD_SHARED_LIBS=1 \
           -DUSE_LTO=1
  make
}

package() {
  cd "$srcdir/$pkgname/$pkgname-build"
  make DESTDIR="$pkgdir/" install
}
