# Maintainer: Daniel Scheller <d.scheller@gmx.net>

_version=20.3.16
_commitdate=20240107
_commitid=4f56bc19a479f467bf326d9ec4bd47f12da6c1ae
_bento4_version=1.6.0-639-7-Omega

pkgname=kodi-addon-inputstream-adaptive-nexus
pkgver=${_version}+git${_commitdate}.${_commitid:0:7}
pkgrel=1
pkgdesc="Inputstream Adaptive addon for Kodi"
arch=('x86_64' 'i686' 'aarch64' 'armv7h' 'armv6h')
url="https://github.com/xbmc/inputstream.adaptive"
license=('GPL2')
depends=('kodi-nexus' 'expat')
makedepends=('kodi-nexus-dev' 'git' 'cmake' 'gtest')
provides=("${pkgname%-nexus}")
conflicts=("${pkgname%-nexus}" "${pkgname%-nexus}-git")
# kodi 20
source=("$pkgname::git+https://github.com/xbmc/inputstream.adaptive.git#commit=$_commitid"
	"bento4-$_bento4_version.tar.gz::https://github.com/xbmc/Bento4/archive/refs/tags/$_bento4_version.tar.gz"
	"gcc13.patch"
)
b2sums=(
	'SKIP'
	'0636f0c94ebb3246c8720e120f3b505e4a82ed2e37d9ed44351875dcf3fab8bc2e53b83e0be43fb711aac3144d6df3d8ae2ead24fcd55bd116f3c54f07e3c19f'
	'4858e4bfc0f4e300bd25eda010571c76e9824f436734b2cfd7415641babe5324362e7819766ce5234f48fa5f8a2390bd608ed962ef881370f485a995fac0425c'
)

prepare() {
  # Fixup build with GCC 13
  echo
  echo `pwd`
  ls -al

  cd "$pkgname" && patch -p1 -i "$srcdir/gcc13.patch"
}

build() {
  mkdir -p "$srcdir/$pkgname/$pkgname-build"
  cd "$srcdir/$pkgname/$pkgname-build"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_BUILD_TYPE=Release \
           -DBUILD_SHARED_LIBS=1 \
           -DENABLE_INTERNAL_BENTO4=ON \
           -DBENTO4_URL="$srcdir/bento4-$_bento4_version.tar.gz" \
           -DUSE_LTO=1
  make
}

package() {
  cd "$srcdir/$pkgname/$pkgname-build"
  make DESTDIR="$pkgdir/" install
}
