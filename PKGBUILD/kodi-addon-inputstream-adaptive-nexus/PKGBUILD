# Maintainer: Daniel Scheller <d.scheller@gmx.net>

_version=20.3.6
_commitdate=20230407
_commitid=89da8a91ba8daa1dc99311d82addb255974ea16b
_bento4_version=1.6.0-639-6-Nexus

pkgname=kodi-addon-inputstream-adaptive-nexus
pkgver=${_version}+git${_commitdate}.${_commitid:0:7}
pkgrel=1
pkgdesc="Inputstream Adaptive addon for Kodi"
arch=('x86_64' 'i686' 'aarch64' 'armv7h' 'armv6h')
url="https://github.com/xbmc/inputstream.adaptive"
license=('GPL2')
depends=('kodi-nexus' 'expat')
makedepends=('kodi-nexus-dev' 'git' 'cmake' 'gtest')
provides=("${pkgname%-nexus}")
conflicts=("${pkgname%-nexus}" "${pkgname%-nexus}-git")
# kodi 20
source=("$pkgname::git+https://github.com/xbmc/inputstream.adaptive.git#commit=$_commitid"
	"bento4-$_bento4_version.tar.gz::https://github.com/xbmc/Bento4/archive/refs/tags/$_bento4_version.tar.gz"
)
b2sums=(
	'SKIP'
	'130e5391abc3f85377a2ef2bfa992769ec188172743e6406fc96b9e561e82e0da57c77585f2cbf3e176228267d4c53920970dd1cbdc784f954130c92f77eaf61'
)

build() {
  mkdir -p "$srcdir/$pkgname/$pkgname-build"
  cd "$srcdir/$pkgname/$pkgname-build"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_BUILD_TYPE=Release \
           -DBUILD_SHARED_LIBS=1 \
           -DENABLE_INTERNAL_BENTO4=ON \
           -DBENTO4_URL="$srcdir/bento4-$_bento4_version.tar.gz" \
           -DUSE_LTO=1
  make
}

package() {
  cd "$srcdir/$pkgname/$pkgname-build"
  make DESTDIR="$pkgdir/" install
}
